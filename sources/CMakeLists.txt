
add_subdirectory(scenes)
add_subdirectory(headers)

message(STATUS "----------------------------------------------------------------")
message(STATUS "cage-tests")
message(STATUS "----------------------------------------------------------------")

macro(cage_tests category folder)
	string(LENGTH "${CMAKE_CURRENT_SOURCE_DIR}/${folder}" folderLength)
	file(GLOB cage-tests-source-list "${folder}/*.cpp" "${folder}/*/*.cpp")
	file(GLOB cage-tests-header-list "${folder}/*.h")
	foreach(test ${cage-tests-source-list})
		string(LENGTH ${test} testLength)
		get_filename_component(name ${test} NAME)
		string(LENGTH ${name} nameLength)
		string(REPLACE ".cpp" "" name ${name})
		add_executable(${name} ${test} ${cage-tests-header-list})
		target_link_libraries(${name} cage-engine)
		math(EXPR subcategoryLength ${testLength}-${folderLength}-${nameLength}-1)
		string(SUBSTRING ${test} ${folderLength} ${subcategoryLength} subcategory)
		cage_ide_category(${name} ${category}/${subcategory})
		cage_ide_working_dir_in_place(${name})
	endforeach()
endmacro()

cage_tests(client "client")
cage_tests(gui "gui")
cage_tests(engine "engine")
